CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE ARCHON
C FINDS THE BOUND STATES OF THE LENNARD-JONES POTENTIAL
C FROM THE BOHR-SOMMERFELD QUATIZATION RULE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C GLOBAL VARIABLES
      INCLUDE 'IO.ALL'
      INCLUDE 'PARAM.E1'
C LOCAL VARIABLES
      REAL S                 ! CURRENT VALUE OF ACTION
      REAL E1                ! CURRENT VALUE OF ENERGY
      REAL X1, X2            ! CURRENTS TURNING POINTS
      REAL F1                ! F=ACTION/2-PI/2-ILEVEL*PI
      INTEGER ILEVEL         ! CURRENT LEVEL
      REAL ENERGY(0:MAXLVL)  ! ENERGY OF BOUND STATE
      REAL XIN(0:MAXLVL)     ! INNER TURNING POINT
      REAL XOUT(0:MAXLVL)    ! OUTER TURNING POINT
      INTEGER NLINES         ! NUMBER OF POINTS PRINTED TO TERMINAL
      INTEGER SCREEN         ! SEND TO TERMINAL
      INTEGER PAPER          ! MAKE A HARDCOPY
      INTEGER FILE           ! SEND TO A FILE
      DATA SCREEN,PAPER,FILE/1,2,3/
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     OUTPUT SUMMARY OF PARAMETERS
      IF (TTERM) CALL PRMOUT(OUNIT,NLINES)
      IF (TFILE) CALL PRMOUT(TUNIT,NLINES)
      IF (GFILE) CALL PRMOUT(GUNIT,NLINES)
C
C     SEARCH FOR BOUND STATES
      E1=-1.                 ! BEGIN AT THE WELL BOTTOM
      F1=-PI/2               ! THE ACTION IS ZERO THERE
C
C     FIND THE NLEVEL BOUND STATES
      DO 100 ILEVEL=0,NLEVEL-1
C
         CALL SEARCH(ILEVEL,E1,F1,X1,X2)  ! SEARCH FOR EIGENVALUE
         ENERGY(ILEVEL)=E1                ! STORE VALUES FOR THIS STATE
         XIN(ILEVEL)=X1
         XOUT(ILEVEL)=X2
C
C        TEXT OUTPUT
         IF (TTERM) CALL TXTOUT(OUNIT,ILEVEL,E1,X1,X2,NLINES)
         IF (TFILE) CALL TXTOUT(TUNIT,ILEVEL,E1,X1,X2,NLINES)
C
         F1=F1-PI            ! GUESS TO BEGIN SEARCH FOR NEXT LEVEL
C
  100 CONTINUE
C
      IF (TTERM) CALL PAUSE('TO CONTINUE...',1)
      IF (TTERM) CALL CLEAR
C
C     GRAPHICS OUTPUT
      IF (GTERM) CALL GRFOUT(SCREEN,ENERGY,XIN,XOUT)
      IF (GFILE) CALL GRFOUT(FILE,ENERGY,XIN,XOUT)
      IF (GHRDCP) CALL GRFOUT(PAPER,ENERGY,XIN,XOUT)
C
      RETURN
      END

