      SUBROUTINE BND(I,J,K,V,F,LL)
C
C     AUXILIARY-LIBRARY-ROUTINE FOR RELAX3D
C
C                                                05/AUG/1980
C                                                C.J. KOST SIN
C      
C   GENERAL BND ROUTINE FOR RELAX3D TO SET BOUNDARY
C   BY READING FROM UNIT 4 A SET OF (I,J) POINTS
C   PAIRED IN LEFT-RIGHT EDGES OF THE REGION
C   EACH REGION BEING SPECIFIED BY A SINGLE CARD
C   SPECIFYING THE 'K' & 'V'  (2 ENTRIES ONLY
C   TERMINATED BY A "/".
C
C   SET FOR MAXIMUM OF 80 REGIONS , 200 POINTS/REGION
C
C   NOTE:  THIS SUBROUTINE HAS BEEN MODIFIED BY S. HOLLAND
C          FOR 100 REGIONS AND 300 POINTS/REGION.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      INTEGER*2 IJ(300,2,100),IJR(300,2)
C
C   NIJ( ) RECORDS # POINTS/REGION
C
      INTEGER NIJ(100)
C
C   NKV( , ) RECORDS K & V FOR EACH REGION.
C
      REAL NKV(100,2)
      LOGICAL FIRST
      DATA FIRST/.TRUE./
      IF(.NOT. FIRST) GO TO 200
      NREG=1
 120  NPTS=1
      JOLD=1
      DO 155 N=1,300
      IJR(N,1)=0
      IJR(N,2)=0
 155  CONTINUE
      READ(4,*,ERR=299,END=199)NKV(NREG,1),NKV(NREG,2)
      READ(4,*,ERR=299,END=199)(IJR(N,1),IJR(N,2),N=1,300)
 160  IF(IJR(NPTS,1) .EQ. 0) GO TO 170
      IJ(NPTS,1,NREG)=IJR(NPTS,1)
      IJ(NPTS,2,NREG)=IJR(NPTS,2)
      IF(NPTS .NE. (NPTS/2)*2) GO TO 165
C
C   CHECK IF J MONOTONIC
C
      IF(IJR(NPTS,2) .LT. JOLD) WRITE(6,162)NREG,NPTS
 162  FORMAT(' J NOT MONOTONIC IN REGION ',I5,' POINT= ',I3)
      JOLD=IJR(NPTS,2)
C
C   CHECK IF LAST & CURRENT POINT HAVE SAME J
C   IF NOT THEN WRITE WARNING MESSAGE.
C
      IF(IJR(NPTS,2) .NE. IJR(NPTS-1,2))WRITE(6,167)NREG,NPTS
 167  FORMAT(' J PAIRING MISMATCH IN REGION',I5,' POINT=',I3)
C
C   CHECK IF I PAIRS ARE MONOTONIC INCREASING
C
      IF(IJR(NPTS,1).LT.IJR(NPTS-1,1))WRITE(6,168)NREG,NPTS,
     # IJR(NPTS,1),IJR(NPTS,2)
 168  FORMAT(' I PAIRING NOT MONOTONIC IN REGION',I5,
     C      ' POINT#=',I4,' VALUES=',2I6 )
 165  NPTS=NPTS+1
      GO TO 160
 170  NPTS=NPTS-1
      IF(NPTS .NE. (NPTS/2)*2) WRITE(6,100)NREG
 100  FORMAT(' REGION',I5,' ODD PAIR DATA ERROR')
      NIJ(NREG)=NPTS
      NREG=NREG+1
      GO TO 120
 199  NREG=NREG-1
C
C   CHECK ALL I POINTS FOR MONOTOTIC INCREASING (J FIXED)
C
      DO 140 N=1,NREG
      DO 139 M=2,NIJ(N)
      I1=IJ(M-1,1,N)
      I2=IJ(M,1,N)
      J1=IJ(M-1,2,N)
      J2=IJ(M,2,N)
      IF(J1.NE.J2) GO TO 139
      IF(I1.GT.I2)WRITE(6,169)N,M,I1,I2,J1,J2
 169  FORMAT(' I PAIRING NOT MONOTONIC IN REGION',I5,
     C      ' POINT#=',I4,' VALUES=',4I6 )
 139  CONTINUE
 140  CONTINUE
      NIR=1
      FIRST=.FALSE.
 200  IV=16000
C
C   SCAN I,J ONLY WHEN NKV(REGION,1)=K
C
      DO 10 NR=NIR,NREG
      IF(K .NE. NKV(NR,1)) GO TO 10
C
C    SET REGION POTENTIAL
C
      V=NKV(NR,2)
C
C   CHECK IF J IN THE RANGE IF J's OF THIS REGION
C
      IF(J .LT. IJ(1,2,NR)) GO TO 10
      IF(J .GT. IJ(NIJ(NR),2,NR)) GO TO 10
C
C   SCAN UNTIL J MATCHES
C
      DO 20 NJ=1,NIJ(NR)/2
      NJ2=NJ*2-1
      IF(J .NE. IJ(NJ2,2,NR) ) GO TO 20
C
C  K,J MATCH --  SEE IF I BRACKETED
C
      IF(I.GE.IJ(NJ2,1,NR) .AND. I.LE.IJ(NJ2+1,1,NR)) RETURN
  20  CONTINUE
  10  CONTINUE
      V=.5
      RETURN
 299  WRITE(6,450)NREG
 450  FORMAT(' ERROR IN REGION # ',I5)
      RETURN
      END
